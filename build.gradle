import com.sun.org.apache.xalan.internal.xsltc.compiler.Copy

buildscript {
    repositories {
        mavenLocal()
	mavenCentral()
        jcenter()
        maven {
            url "https://oss.jfrog.org/artifactory/oss-snapshot-local"
        }
    }
    dependencies {
        classpath group: 'org.intermine', name: 'plugin', version: System.getProperty("imVersion")
        classpath group: 'com.jfrog.bintray.gradle', name: 'gradle-bintray-plugin', version: '1.8.5'
        classpath group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.25.2'
    }
}

ext {
    // set here to make available to individual bio-sources
    bioVersion = System.getProperty("bioVersion")
    imVersion = System.getProperty("imVersion")
}

subprojects {
    group = 'org.intermine'
    version = '5.0.7'

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'dbmodel'
    apply plugin: 'biosource-dbmodel'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
	mavenCentral()
        jcenter()
        maven {
            url "https://oss.jfrog.org/artifactory/oss-snapshot-local"
        }
    }

    configurations {
        bioModel
        mergeModel
    }

    dependencies {
        compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.17.1'
        compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.1'
        compile group: 'org.intermine', name: 'bio-core', version: System.getProperty("bioVersion"), transitive: false
        compile group : "org.intermine", name: "intermine-resources", version: System.getProperty("imVersion") // log4j
        compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
        testCompile group: 'junit', name: 'junit', version: '4.13.2'
        testCompile group: "org.intermine", name: "intermine-integrate", version: System.getProperty("imVersion"), classifier: "testClasses"
        testCompile group: 'xmlunit', name: 'xmlunit', version: '1.6'
        mergeModel group : "org.intermine", name: "intermine-objectstore", version: System.getProperty("imVersion")
    }

    dbModelConfig {
        objectStoreName = "os.bio-test"
        modelName = "genomic"
        defaultInterminePropertiesFile = "default.intermine.bio-test.properties"
    }

    task copyMineProperties() {
        dependsOn 'processResources'
        doLast {
            copy {
                from "${System.env.HOME}/.intermine/thalemine.properties"
                into sourceSets.main.output.resourcesDir
                rename { fileName -> fileName.replace('intermine-bio-test.properties', 'thalemine.properties') }
            }
        }
    }

    jar {
        exclude "intermine.properties"
    }

    // see http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    tasks.withType(Test) {
        scanForTestClasses = false
        include '**/*Test.class'
    }

    test {
        systemProperties System.properties
        ignoreFailures = true
        forkEvery = 1
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    tasks.compileTestJava.dependsOn 'copyMineProperties'

}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}
